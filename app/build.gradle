apply plugin: 'com.android.application'
apply plugin: 'android-apt'

Properties prop = new Properties()
prop.load(new FileInputStream(file("signing.properties")))

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.0"

    defaultConfig {
        applicationId "com.sun.bingo"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 5
        versionName "1.3.1"
        manifestPlaceholders = [umengHolder: "DEBUG"]
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    signingConfigs {
        release {
            storeFile file(prop['KEYSTORE_FILEPATH'])
            storePassword prop['STORE_PASSWORD']
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            //压缩资源
            shrinkResources true
            //zip优化
            zipAlignEnabled true
            //apk签名
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                appendVersionNameVersionCode(variant, defaultConfig)
            }
        }

        debug{
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        aRelease {manifestPlaceholders = [umengHolder: "aRelease"]}
        m360 {manifestPlaceholders = [umengHolder: "m360"]}
        baidu {manifestPlaceholders = [umengHolder: "baidu"]}
        qq {manifestPlaceholders = [umengHolder: "qq"]}
        googleplay {manifestPlaceholders = [umengHolder: "googleplay"]}
        wandoujia {manifestPlaceholders = [umengHolder: "wandoujia"]}
        m91 {manifestPlaceholders = [umengHolder: "m91"]}
        anzhi {manifestPlaceholders = [umengHolder: "anzhi"]}
        xiaomi {manifestPlaceholders = [umengHolder: "xiaomi"]}
        huawei {manifestPlaceholders = [umengHolder: "huawei"]}
        lenovo {manifestPlaceholders = [umengHolder: "lenovo"]}
        oppo {manifestPlaceholders = [umengHolder: "oppo"]}
        meizu {manifestPlaceholders = [umengHolder: "meizu"]}
    }

}

def appendVersionNameVersionCode(variant, defaultConfig) {
    variant.outputs.each { output ->
        if (output.zipAlign) {
            def file = output.outputFile
            def fileName = file.name.replace(".apk", "_" + defaultConfig.versionName +".apk").replaceFirst("app","bingo_world").replace("-","_")
            output.outputFile = new File(file.parent, fileName)
        }

        def file = output.packageApplication.outputFile
        def fileName = file.name.replace(".apk", "_" + defaultConfig.versionName  + ".apk").replaceFirst("app","bingo_world").replace("-","_")
        output.packageApplication.outputFile = new File(file.parent, fileName)
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.jakewharton:butterknife:5.1.2'

    compile 'com.android.support:appcompat-v7:22.2.+'
    compile 'com.android.support:cardview-v7:22.2.+'
    compile 'com.android.support:recyclerview-v7:22.2.+'
    compile 'com.android.support:palette-v7:22.2.+'
    compile 'com.android.support:design:22.2.+'
    compile 'com.android.support:gridlayout-v7:22.2.+'

    compile project(':extras-libs:ImageLoader') //ImageLoader
    compile project(':extras-libs:orm-library') //Afinal DataBase

    //multidex
    compile 'com.android.support:multidex:1.0.0'

    //SharedPreferences
    apt 'de.devland.esperandro:esperandro:2.1.0'
    compile 'de.devland.esperandro:esperandro-api:2.1.0'

    //SystemBarTint
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'

    compile 'com.nineoldandroids:library:2.4.0'
    compile 'cn.bingoogolapple:bga-refreshlayout:1.0.4@aar'

    //Material Dialog
    compile 'com.afollestad:material-dialogs:0.7.6.0'

    //Floating Action Button
    compile 'com.shamanland:fab:0.0.8'

    //Shape Image View
    compile 'com.github.siyamed:android-shape-imageview:0.9.+@aar'

    //Logger
    compile 'com.orhanobut:logger:1.4'

    //MaterialEditText
    compile 'com.rengwuxian.materialedittext:library:2.1.4'

    //GoogleProgressBar
    compile 'com.jpardogo.googleprogressbar:library:1.2.0'

    //SmoothProgressBar
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'

    //RichEditor
    compile 'jp.wasabeef:richeditor-android:0.1'

    //Facebook Fresco
    compile 'com.facebook.fresco:fresco:0.6.0'

    //PhotoView
    compile 'com.github.chrisbanes.photoview:library:1.2.4'

    //LoadingView
    compile 'com.github.zzz40500:android-shapeLoadingView:1.0.3.2'

    //Stetho Chrome debug app
    compile 'com.facebook.stetho:stetho:1.1.1'

    //EventBus
    compile 'de.greenrobot:eventbus:2.4.0'
}
